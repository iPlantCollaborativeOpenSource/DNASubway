<div style="width: 80%;">

% if (@err) {
<div class="error" style="color: red; ">
%	for (@err) {
		<div><% $_ %></div>
%	}
</div>
%}

<form method="post">
	<table>
%	for (@$fields) {
%		my $type = defined $_->{type} ? $_->{type}  : 'text';
		<tr>
			<td><% $_->{label} %>:</td>
			<td><input type="<% $type %>" name="<% $_->{id} %>" value="<% $s->{$_->{id}} |html%>" maxlength="<% $_->{maxlength} %>"/></td>
		</tr>
%	}

		<tr>
			<td>&nbsp;</td>
			<td><input type="submit" name="submit" value="Register" /></td>
		</tr>
	</table>
</form>
</div>

%#-------------------------------------------------------------------
<%once>
	use DNALC::Pipeline::Utils qw/random_string/;
	use DNALC::Pipeline::Config ();
	use DNALC::Pipeline::Chado::Utils ();
	use DNALC::Pipeline::User ();
	use DNALC::Pipeline::Group ();
	use Email::Valid ();
	use MIME::Lite ();

	my $fields = [
			{ id => "name_first", label => "First name", re => qr/.+/, maxlength => 64},
			{ id => "name_last", label => "Last name", re => qr/.+/, maxlength => 64},
			{ id => "username", label => "Username", re => qr/^[a-z0-9_-]+$/i, maxlength => 16},
			{ id => "email", type => "email", label => "Email", re => qr/\@/, maxlength => 120},
		];
</%once>
%#-------------------------------------------------------------------
<%init>
$m->session->{pipeline_reg} ||= {};
my $s = $m->session->{pipeline_reg};

my @err = ();

if ($r->method eq "POST") {

	my $cf = DNALC::Pipeline::Config->new->cf('PIPELINE');

	for (@$fields) {
		my $f = $_->{id};
		$s->{$f} = substr $ARGS{$f}, 0, $_->{maxlength} if defined $ARGS{$f};
		unless (defined $s->{$f} && $s->{$f} =~ $_->{re}) {
			push @err, "$_->{label} is missing or invalid!";
		}
	}
	
	if ($s->{username} && length($s->{username}) < 4) {
		push @err, "Username is too short. Use at least 4 chars.";
	}
	
	unless (@err) {
		unless (Email::Valid->address($s->{email})) {
			push @err, "Email address is invalid!";
		}
		my $user = DNALC::Pipeline::User->search( username => $s->{username});
		if ($user || $s->{username} =~ /^(?:chado|guest|default|postgre|template)/i) {
			push @err, "Username not available!";
		}
		else {
			my $cutils = DNALC::Pipeline::Chado::Utils->new(
							username => $s->{username},
							profile => $cf->{GMOD_PROFILE},
						);
			if ($cutils->check_db_exists($s->{username})) {
				push @err, "Username not available!";
			}
			$cutils->dbh->disconnect;
		}
	}
	
	unless (@err ) {
		# create passwd
		my $pwd = random_string(4, 15);

		# create account		
		my $u = eval {
				DNALC::Pipeline::User->create({
					username => $s->{username},
					password => $pwd,
					email => $s->{email},
					name_first => $s->{name_first},
					name_last => $s->{name_last}
				});
			};

		if ($@) {
			print STDERR "USER: ------------------------------------\n";
			print STDERR $@, "\n";
			print STDERR "USER: ------------------------------------\n";
			push @err, "Error creating user!";
		}
		else {
			# launch "create-chado-db" in the background

			my ($users_group) = DNALC::Pipeline::Group->search(group_name => "user");
			$u->add_to_group($users_group) if $users_group;

			my $code = $u->create_reset_pwd_code;
			$u->set_reset_pwd_code($code);

			if ($code) {
				my $text = "Dear user,\n
Thank you for creating an account.\n
Your unsername is: \"$s->{username}\" (without the quotes).\n
To set your password follow this link:
$cf->{APOLLO_PROJECT_HOME}/resetpass.html?c=$code\n
DNALC Team";

				# send mail to the applicant
				my $msg = MIME::Lite->new (
							From => '"Admin" <dnalcadmin@cshl.edu>',
							To	 => $s->{email},
							Subject => "Set your password",
							Type	=> 'text/plain',
							Data => $text
						);
				eval {
					$msg->send("smtp", $cf->{SMTP_SERVER}, Timeout=>30);
					
				};
				if ($@) {
					print STDERR "Error: " . $@ . "\n";
					$msg->send();
				}
			}			

			$m->comp("/_message_add", "Account created. You will receive an email with further instructions.");

			# clear registration session
			$m->session->{pipeline_reg} = {};
			$m->redirect("/");
		}

	}
}

</%init>
