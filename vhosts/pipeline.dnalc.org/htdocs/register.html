<div style="width: 80%;">

% if ($faction ne 'reload' && @err) {
<div style="display:none" id="error_list">
%	for (@err) {
		<div><% $_ %></div>
%	}
</div>
%}

<form id="form_reg" method="post">
<input type="hidden" name="faction" id="faction" value="" />
	<table class="register-table">
%	if (0) {
		<tr>
			<td class="label">Title:</td>
			<td><select name="title" id="title" class="conStylized_box4"/>
		<option value="">Title</option>
%	for ( @titles ) {
%		my $sel = lc $_ eq $s->{title} ? " selected=\"selected\"" : "";
	<option value="<% lc $_ %>"<% $sel %>><% $_ %></option>
%	}
			</select></td>
		</tr>
%	}
%	for (@$fields) {
%		my $type = defined $_->{type} ? $_->{type}  : 'text';
		<tr>
			<td class="label"><% $_->{label} %>:</td>
			<td><input class="conStylized_box4" type="<% $type %>" name="<% $_->{id} %>" value="<% $s->{$_->{id}} |html%>" maxlength="<% $_->{maxlength} %>"/></td>
		</tr>
%	}
		<tr>
			<td colspan="2">&nbsp;</td>
		</tr>
		<tr>
			<td class="label">Country:</td>
			<td><& project/.comp/countries, country => $s->{country}, class => "conStylized_box4" &></td>
		</tr>
		<tr>
			<td class="label">Zip code:</td>
			<td><input class="conStylized_box4" type="text" name="zip" value="<% $s->{zip} |html%>" maxlength="10"/></td>
		</tr>
<& "./_user_profile", parent => $tree_root, questions => $questions, answers => $s &>
	</table>
	<div style="text-align: center"><input type="submit" name="fsubmit" value="Register" /></div>
</form>
</div>

%#-------------------------------------------------------------------
<%args>
	$title => ''
	$gender => ''
	$country => ''
	$zip => ''
	$degree => ''
	$degree_details => ''
	$occupation => ''
	$ethnicity => ''
	$faction => ''
</%args>
%#-------------------------------------------------------------------
<%once>
	use DNALC::Pipeline::Utils qw/random_string/;
	use DNALC::Pipeline::Config ();
	use DNALC::Pipeline::Chado::Utils ();
	use DNALC::Pipeline::User ();
	use DNALC::Pipeline::Group ();
	use DNALC::Pipeline::UserProfile ();
	use DNALC::Pipeline::UserLDAP ();
	use Email::Valid ();
	use MIME::Lite ();
	
	use Data::Dumper;
	
	my $VALUE_MAX_LEN = 2000; # max 2000 chars (what about utf8 ?!)

	my $fields = [
			{ id => "name_first", label => "First name", re => qr/.+/, maxlength => 64},
			{ id => "name_last", label => "Last name", re => qr/.+/, maxlength => 64},
			{ id => "username", label => "Username", re => qr/^[a-z0-9_-]+$/i, maxlength => 16},
			{ id => "email", type => "email", label => "Email", re => qr/\@/, maxlength => 120},
			{ id => "pwd", type => "password", label => "Password", re => qr/.{5,}/, maxlength => 20, 
						msg => "Password is missing or is shorter then 5 chars."},
			{ id => "repwd", type => "password", label => "Re-type password", re => qr/.+/, maxlength => 20},
		];
	my @titles = qw/Ms Mr Miss PhD MBA MD Prof/;
	my %question_map = (
			country => 'q6',
			zip	=> 'q8',
		);
</%once>
%#-------------------------------------------------------------------
<%init>
$m->session->{pipeline_reg} ||= {};
my $s = $m->session->{pipeline_reg};

my @err = ();

if ($faction ne "") {
	push @err, "";
}

my $tree_root = 2;
my $questions = undef;#$m->cache->get("question-tree-$tree_root");
unless (defined $questions) {
		$questions = DNALC::Pipeline::UserProfile->get_question_tree($tree_root);
		$m->cache->set("question-tree-$tree_root", $questions, "2m"); # set cache for X minutes
}
else {
	#print STDERR "cache: ", "question-tree-$tree_root", $/;
}

#print STDERR "-" x 20, $/;
#print STDERR "Session:\t", Dumper($s), $/;

if ($r->method eq "POST") {

	my $user_profile_data = {};
	#print STDERR "\t", Dumper(\%ARGS), $/;
	for (keys %ARGS) {
		$s->{$_} = substr $ARGS{$_}, 0, $VALUE_MAX_LEN;
		$user_profile_data->{$_} = $s->{$_} if $_ =~ /^q\d+/;
	}
	#print STDERR "User profile data:\t", Dumper($user_profile_data), $/;

	my $cf = DNALC::Pipeline::Config->new->cf('PIPELINE');

	for (@$fields) {
		my $f = $_->{id};
		$s->{$f} = substr $ARGS{$f}, 0, $_->{maxlength} if defined $ARGS{$f};
		unless (defined $s->{$f} && $s->{$f} =~ $_->{re}) {
			push @err, $_->{msg} || "$_->{label} is missing or invalid!";
		}
	}
	
	if ($s->{username} && length($s->{username}) < 3) {
		push @err, "Username is too short. Use at least 3 chars.";
	}
	elsif ($s->{username} =~ /[A-Z]/) {
		push @err, "Use lowecase letters for you username.";
	}

	if ($s->{pwd} && $s->{pwd} ne $s->{repwd}) {
		push @err, "Passwords to not match.";
	}
	
	if (! defined $s->{country}  || $s->{country} eq '') {
		push @err, "Country is missing.";
	}
	if (!defined $s->{zip} || $s->{zip} eq '') {
		push @err, "Zip/Postal code is missing.";
	}

	unless (Email::Valid->address($s->{email})) {
		push @err, "Email address is invalid!";
	}

	unless (@err) {
		my $user = DNALC::Pipeline::User->search( username => $s->{username});
		if ($user || $s->{username} =~ /^(?:chado|guest|default|postgre|temp|mysql)/i || $s->{username} =~ /^de$/) {
			push @err, "Username not available!";
		}
		else {
			my $cutils = DNALC::Pipeline::Chado::Utils->new(
							username => $s->{username},
							profile => $cf->{GMOD_PROFILE},
						);
			if ($cutils->check_db_exists($s->{username})) {
				push @err, "Username not available!";
			}
			$cutils->dbh->disconnect;
		}

		# check ldap 
		if (!@err && $cf->{LDAP_ENABLED}) {
			my $ldap_u = DNALC::Pipeline::UserLDAP->search($s->{username});
			if ($ldap_u) {
				push @err, "Username not available!";
			}
		}
	}
	
	unless (@err ) {
		# create account		
		my $u = eval {
				DNALC::Pipeline::User->create({
					username => $s->{username},
					password => $s->{pwd},
					email => $s->{email},
					title => $s->{title},
					name_first => $s->{name_first},
					name_last => $s->{name_last},
					login_count => 1,
				});
			};

		if ($@) {
			print STDERR "USER: ------------------------------------\n";
			print STDERR $@, "\n";
			print STDERR "USER: ------------------------------------\n";
			push @err, "Error creating user!";
		}
		else {
			# store country && zip code
			for (keys %question_map ) {
				print STDERR "DD: ", $_, " = ", $question_map{$_}, " = ", $s->{$_}, $/;
			}
			my %data_directly = map {$question_map{$_} => $s->{$_}} grep {$s->{$_} ne "" } keys %question_map;
			eval {
				DNALC::Pipeline::UserProfile->store_user_profile_directly($u->id, \%data_directly);
			};
			
			# store the rest of the profile questions
			eval {
				DNALC::Pipeline::UserProfile->store_user_profile($tree_root, $u->id, $user_profile_data);
			};
			if ($@) {
				print STDERR "USER PROFILE: ------------------------------------\n";
				print STDERR $@, "\n";
				print STDERR "USER PROFILE: ------------------------------------\n";
			}
			
			# launch "create-chado-db" in the background

			my ($users_group) = DNALC::Pipeline::Group->search(group_name => "user");
			$u->add_to_group($users_group) if $users_group;

			if (1) {
				my $text = "Welcome to DNA Subway, Fast Track to Gene Annotation and Genome Analysis.\n
Your username is: \"$s->{username}\" (without the quotes).\n
For information on using the site, take advantage of the DNA Subway Tour
and DNA Subway Manual, linked from the home page and the site footer.\n

Sincerely,
DNASubway Team\n";

				my $from_email = $cf->{ADMIN_EMAIL} || '"DNASubway Admin" <dnalcadmin@cshl.edu>';
				# send mail to the applicant
				my $msg = MIME::Lite->new (
							From => $from_email,
							To	=> $s->{email},
							Subject => "Thank you for registering on DNA Subway!",
							Type	=> "text/plain",
							Data	=> $text
						);
				eval {
					if (defined $cf->{SMTP_SERVER} && $cf->{SMTP_SERVER} ne "") {
						$msg->send("smtp", $cf->{SMTP_SERVER}, Timeout=>30);
					}
					else {
						$msg->send;
					}
				};
				if ($@) {
					print STDERR "Error: " . $@ . "\n";
					$msg->send;
				}
			}			

			$m->comp("/_message_add", "<p>Welcome to DNA Subway!</p>");
			#				"In order to use your new account go to your email"
			#			. " and follow the instructions in the message you receive from <u>dnalcadmin\@cshl.edu</u>.</p>";
			#			. "<br/><p>Should you not get this message it may have been filtered out by your"
			#			. " provider's spam/junk filters.</p>");

			# clear registration session
			$m->session->{pipeline_reg} = {};
			
			# log them in
			$s = $m->session->{pipeline};

			$s->{username} = $u->username;
			$s->{full_name} = $u->full_name;
			$s->{user_id} = $u->id;
			$s->{institution} = DNALC::Pipeline::UserProfile->get_user_institution($u->id);
			$s->{logged_in} = 1;
			$s->{new_user} = 1;
			
			$m->redirect("/");
		}
	}
}

</%init>

<%attr>
	js => ['user_profile.js']
</%attr>
%#-------------------------------------------------------------------
<%method title>\
Register - 
</%method>\
%#-------------------------------------------------------------------
